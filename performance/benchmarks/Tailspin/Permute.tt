include 'harness'

// I don't know what this benchmark really is supposed to calculate, but it sure does a lot of swaps
templates permute
  templates derange
    @permute.count: $@permute.count + 1;
    $ -> #
    <1..>
      def n: $;
      templates swap
        def i: $;
        def temp: $@permute.v($n);
        @permute.v($n): $@permute.v($i);
        @permute.v($i): $temp;
      end swap
      $n - 1 -> derange -> !VOID
      $n..1:-1 -> \($ -> swap -> !VOID
        $n - 1 -> derange -> !VOID
        $ -> swap -> !VOID\) -> !VOID
  end derange
  @: { count: 0, v: [1..$]};
  $ -> derange -> !VOID
  $@.count !
end permute

templates runAndVerify
  6 -> permute -> #
  <=8660> 1 !
  <> 0 !
end runAndVerify

1 -> harness/run@{name: 'Permute', benchmark:runAndVerify} -> !VOID
