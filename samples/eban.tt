// An eban number is a number that does not contain any letter 'e' when written out in american english
// Only numbers below one sextillion are considered

templates isEban1
  def number: $;
  '$;' -> \(<'([246]|[3456][0246])(0[03456][0246])*$$'> $ !\) -> $number !
end isEban1

//Alternatively with numbers
templates isEban
  def number: $;
  $ -> \(<1..> $!\) -> #
  <=0> $number !
  <?($ mod 1000 <=0|=2|=4|=6|30..66?($ mod 10 <=0|=2|=4|=6>)>)> $ ~/ 1000 -> #
end isEban

def small: [1..1000 -> isEban];
$small -> !OUT::write
'
There are $small::length; eban numbers up to and including 1000

' -> !OUT::write

def next: [1000..4000 -> isEban];
$next -> !OUT::write
'
There are $next::length; eban numbers between 1000 and 4000 (inclusive)

' -> !OUT::write
'
There are $:[1..10000 -> isEban] -> $::length; eban numbers up to and including 10 000

' -> !OUT::write
'
There are $:[1..100000 -> isEban] -> $::length; eban numbers up to and including 100 000

' -> !OUT::write
'
There are $:[1..1000000 -> isEban] -> $::length; eban numbers up to and including 1 000 000

' -> !OUT::write
'
There are $:[1..10000000 -> isEban] -> $::length; eban numbers up to and including 10 000 000

' -> !OUT::write
